🌮
    🔳 is a basic rectangle.
    useful for collisions.
    Rectangles have:
        x y w (width) h (height) and static?
    static? is used during collisions to determen
        how to move objects.
🌮
👴 There is no absulote value in Emojicode
🐋 🚄 🍇
    🐇🐖 🙌 n 🚂 ➡️ 🚂 🍇
        🍊 ◀️ n 0 🍇
            🍎 ➖ 0 n
        🍉
        🍓 🍇
            🍎 n
        🍉
    🍉
🍉
🐇 🔳 🍇
    🍰 x 🚀
    🍰 y 🚀
    🍰 w 🚂
    🍰 h 🚂
    🍰 hw 🚂
    🍰 hh 🚂
    🍰 vx 🚀
    🍰 vy 🚀
    🍰 physic_type 🔡
    🐈 🆕 @x 🚀 @y 🚀 @w 🚂 @h 🚂 @physic_type 🔡 🍇
        🍮 x @x
        🍮 y @y
        🍮 w @w
        🍮 h @h
        🍮 hw ➗ @w 2
        🍮 hh ➗ @h 2
        🍮 vx 200.0
        🍮 vy 200.0
        🍮 physic_type @physic_type
    🍉
    🐖 📏 ➡️ 🍨🐚⚪ 🍇
        🍎 🍨 x y w h hh hw 🍆
    🍉
    🐖 ✍ @x 🍬🚀 @y 🍬🚀 @w 🍬🚂 @h 🍬🚂 🍇
        🍊 ❎☁️@x 🍇
            🍮 x 🍺@x
            😀 🍪 🔤Setting Y:🔤 🔷🔡🚂 🍩🚴🚄 x 10 🍪
        🍉
        🍊 ❎☁️@y 🍇
            🍮 y 🍺@y
            😀 🍪 🔤Setting Y:🔤 🔷🔡🚂 🍩🚴🚄 y 10 🍪
        🍉
        🍊 ❎☁️@w 🍇
            🍮 w 🍺@w
            🍮 hw ➗ w 2
        🍉
        🍊 ❎☁️@h 🍇
            🍮 h 🍺@h
            🍮 hh ➗ h 2
        🍉
    🍉
    🌮
        returns an object physic type.
    🌮
    🐖 ⚓ ➡️ 🔡 🍇
        🍎 physic_type
    🍉
    🌮
        Find the collision between to objects.
    🌮
    🐖 🛌 other 🔳 ➡ 👌 🍇
        🍰 d_other 🍨🐚⚪
        🍮 d_other 📏other
        👴 Fun fact: 🐚⚪ destroy type information
        👴 Results: Crap-ton of unwrap-downcasting
        🍦 o_x 🍺🔲🍺🐽 d_other 0 🚀
        🍦 o_floor_x 🍩🚴🚄 o_x
        🍦 o_y 🍺🔲🍺🐽 d_other 1 🚀
        🍦 o_floor_y 🍩🚴🚄 o_y
        🍦 o_w 🍺🔲🍺🐽 d_other 2 🚂
        🍦 o_h 🍺🔲🍺🐽 d_other 3 🚂
        🍦 o_hw 🍺🔲🍺🐽 d_other 4 🚂
        🍦 o_hh 🍺🔲🍺🐽 d_other 5 🚂
        🍦 floor_x 🍩🚴🚄 x
        🍦 floor_y 🍩🚴🚄 y
        🍦 dist_x  ➖ ➕ o_floor_x o_hw ➖ floor_x hw
        🍦 dist_y  ➖ ➕ o_floor_y o_hh ➖ floor_y hh
        🍦 sep_x ➕ hh o_hh
        🍦 sep_y ➕ hw o_hw
        🍦 over_x 🍩🙌🚄➖🍩🙌🚄 dist_x sep_x
        🍦 over_y 🍩🙌🚄➖🍩🙌🚄 dist_y sep_y
        🍊 🎊 ➡️ over_x sep_x ➡️ over_y sep_y  🍇
            🍎 👎
        🍉
        🍊 😛 ⚓ other 🔤bat🔤  🍇
            🍊 ◀️ over_x over_y  🍇
                🍊 ◀️ dist_x 0  🍇
                    ✍ 🐕 ➕ x 🚀dist_x ⚡ ⚡ ⚡
                🍉 🍓 🍇
                    ✍ 🐕 ➖ x 🚀dist_x ⚡ ⚡ ⚡
                🍉
            🍉
            🍓 🍇
                🍊 ◀️ dist_y 0  🍇  👴 if dist_y < 0
                    ✍ 🐕 ⚡ ➕ y 🚀dist_y ⚡ ⚡
                🍉 🍓 🍇
                    ✍ 🐕 ⚡ ➖ y 🚀dist_y ⚡ ⚡
                🍉
            🍉
            🍎 👍
        🍉 🍓 🍇
            🍊 ◀️ over_x over_y  🍇
                🍊 ◀️ dist_x 0  🍇
                    ✍ other ➕ o_x 🚀dist_x ⚡ ⚡ ⚡
                🍉 🍓 🍇
                    ✍ other ➖ o_x 🚀dist_x ⚡ ⚡ ⚡
                🍉
            🍉 🍓 🍇
                🍊 ◀️ dist_y 0  🍇  👴 if dist_y < 0
                    ✍ other ⚡ ➕ o_y 🚀dist_y ⚡ ⚡
                🍉 🍓 🍇
                    ✍ other ⚡ ➖ o_y 🚀dist_y ⚡ ⚡
                🍉
            🍉
            🍎 👍
        🍉
    🍉
🍉
🐇 🎾🔳 🍇
    🐖 🖌 renderer 🖌 🍇
        🍦 dementions 📏🐕
        🍦 x 🍩🚴🚄🍺🔲🍺🐽 dementions 0 🚀
        🍦 y 🍩🚴🚄🍺🔲🍺🐽 dementions 1 🚀
        🍦 w 🍺🔲🍺🐽 dementions 2 🚂
        🍦 h 🍺🔲🍺🐽 dementions 3 🚂
        🎨 renderer 255 255 255 255
        ◾ renderer 🔷◾🆕 x y w h
    🍉
🍉
👴 There is no tennis ball emoji... so baseball it is
🐇 ⚾🔳 🍇
    🐖 🖌 renderer 🖌 🍇
        🍦 dementions 📏🐕
        🍦 x 🍩🚴🚄🍺🔲🍺🐽 dementions 0 🚀
        🍦 y 🍩🚴🚄🍺🔲🍺🐽 dementions 1 🚀
        🍦 w 🍺🔲🍺🐽 dementions 2 🚂
        🍦 h 🍺🔲🍺🐽 dementions 3 🚂
        🎨 renderer 255 255 255 255
        ◾ renderer 🔷◾🆕 x y w h
    🍉
🍉
