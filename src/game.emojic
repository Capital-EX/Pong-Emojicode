🌮
    🔳 is a basic rectangle.
    useful for collisions.
    Rectangles have:
        x y w (width) h (height) and static?
    static? is used during collisions to determen
        how to move objects.
🌮
👴 There is no absulote value in Emojicode
🐋 🚄 🍇
    🐇🐖 🙌 n 🚂 ➡️ 🚂 🍇
        🍊 ◀️ n 0 🍇
            🍎 ➖ 0 n
        🍉
        🍓 🍇
            🍎 n
        🍉
    🍉
🍉
🐇 🔳 🍇
    🍰 x 🚀
    🍰 y 🚀
    🍰 w 🚂
    🍰 h 🚂
    🍰 hw 🚂
    🍰 hh 🚂
    🍰 vx 🚀
    🍰 vy 🚀
    🍰 physic_type 🔡
    🍰 reset? 👌
    🐈 🆕 @x 🚀 @y 🚀 @w 🚂 @h 🚂 @physic_type 🔡 @vx 🍬🚀 @vy 🍬🚀 🍇
        🍮 x @x
        🍮 y @y
        🍮 w @w
        🍮 h @h
        🍮 hw ➗ @w 2
        🍮 hh ➗ @h 2
        🍮 vx 0.0
        🍮 reset? 👎
        🍊 ❎☁️@vx 🍇
            🍮 vx 🍺@vx
        🍉
        🍮 vy 0.0
        🍊 ❎☁️@vy 🍇
            🍮 vy 🍺@vy
        🍉
        🍮 physic_type @physic_type
    🍉
    🐖 📏 ➡️ 🍨🐚⚪ 🍇
        🍎 🍨 x y w h hw hh vx vy 🍆
    🍉
    🐖 ↩ ➡ 👌 🍇
        🍎 reset?
    🍉
    🐖 📼 @reset? 👌  🍇
        🍮 reset? @reset?
    🍉
    🐖 ✍ @x 🍬🚀 @y 🍬🚀 @w 🍬🚂 @h 🍬🚂 @vx 🍬🚀 @vy 🍬🚀🍇
        🍊 ❎☁️@x 🍇
            🍮 x 🍺@x
        🍉
        🍊 ❎☁️@y 🍇
            🍮 y 🍺@y
        🍉
        🍊 ❎☁️@w 🍇
            🍮 w 🍺@w
            🍮 hw ➗ w 2
        🍉
        🍊 ❎☁️@h 🍇
            🍮 h 🍺@h
            🍮 hh ➗ h 2
        🍉
        🍊 ❎☁️@vx 🍇
            🍮 vx 🍺@vx
        🍉
        🍊 ❎☁️@vy 🍇
            🍮 vy 🍺@vy
        🍉
    🍉
    🌮
        returns an object physic type.
    🌮
    🐖 ⚓ ➡️ 🔡 🍇
        🍎 physic_type
    🍉
    🌮
        Find the collision between to objects.
    🌮
    🐖 🛌 other 🔳 ➡ 👌 🍇
        🍦 d_other 📏other
        👴 Fun fact: 🐚⚪ destroy type information
        👴 Results: Crap-ton of unwrap-downcasting
        🍦 o_x 🍺🔲🍺🐽 d_other 0 🚀
        🍦 o_floor_x 🍩🚴🚄 o_x
        🍦 o_y 🍺🔲🍺🐽 d_other 1 🚀
        🍦 o_floor_y 🍩🚴🚄 o_y
        🍦 o_w 🍺🔲🍺🐽 d_other 2 🚂
        🍦 o_h 🍺🔲🍺🐽 d_other 3 🚂
        🍦 o_hw 🍺🔲🍺🐽 d_other 4 🚂
        🍦 o_hh 🍺🔲🍺🐽 d_other 5 🚂
        🍦 floor_x 🍩🚴🚄 x
        🍦 floor_y 🍩🚴🚄 y
        🍦 dist_x  ➖ ➕ o_floor_x o_hw ➕ floor_x hw
        🍦 dist_y  ➖ ➕ o_floor_y o_hh ➕ floor_y hh
        🍦 sep_x ➕ hw o_hw
        🍦 sep_y ➕ hh o_hh
        🍦 over_x ➖ sep_x 🍩🙌🚄 dist_x
        🍦 over_y ➖ sep_y 🍩🙌🚄 dist_y
        🍊 🎉 ◀️ over_x 0 ◀️ over_y 0  🍇
            🍎 👎
        🍉
        🍊 ◀️ vx 0.0 🍇
            🍮 vx ➖ vx 0.01
        🍉 🍓 🍇
            🍮 vx ➕ vx 0.01
        🍉
        🍊 ◀️ vy 0.0 🍇
            🍮 vy ➖ vy 0.01
        🍉 🍓 🍇
            🍮 vy ➕ vy 0.01
        🍉
        👴 for the sake of simplicity; assume this is always called by ball.
        🍊 ◀️ over_x over_y  🍇
            🍊 ◀️ dist_x 0  🍇
                ✍ 🐕 ➕ x 🚀over_x ⚡ ⚡ ⚡ ➖ 0.0 vx ⚡
            🍉 🍓 🍇
                ✍ 🐕 ➖ x 🚀over_x ⚡ ⚡ ⚡ ➖ 0.0 vx ⚡
            🍉
        🍉 🍓 🍇
            🍊 ◀️ dist_y 0  🍇
                ✍ 🐕 ⚡ ➕ y 🚀over_y ⚡ ⚡ ⚡ ➖ 0.0 vy
            🍉 🍓 🍇
                ✍ 🐕 ⚡ ➖ y 🚀over_y ⚡ ⚡ ⚡ ➖ 0.0 vy
            🍉
        🍉
        🍎 👍
    🍉
    🐖 🖌 renderer 🖌 🍇
        🎨 renderer 255 255 255 255
        ◾ renderer 🔷◾🆕 🍩🚴🚄 x 🍩🚴🚄 y w h
    🍉
🍉
🐇 🎾🔳 🍇
    🐖 ⌨ e ⌨ 🍇
        😀 🔷 🔡 🔣 🔣e
        🍊 🎊 🎉 😛 🔣e 🔟w 😛 🔣e 🔟W ⬇e 🍇
            😀 🔤True🔤
        🍉 🍓 🍇
            😀 🔤False🔤
        🍉
        🍊 🎊 🎉  😛 🔣e 🔟w 😛 🔣e 🔟W ⬇e 🍇
            ✍ 🐕 ⚡ ⚡ ⚡ ⚡ ⚡ -0.2
        🍉🍋 🎊 🎉 😛 🔣e 🔟s 😛 🔣e 🔟S ⬇e 🍇
            ✍ 🐕 ⚡ ⚡ ⚡ ⚡ ⚡ 0.2
        🍉 🍋 🎉 🎊 🎉 😛 🔣e 🔟s 😛 🔣e 🔟S ❎⬇e 🎊 🎉 😛 🔣e 🔟w 😛 🔣e 🔟W ❎⬇e 🍇
            ✍ 🐕 ⚡ ⚡ ⚡ ⚡ ⚡ 0.0
        🍉
    🍉
    🐖 📈 🍇
        🍦 dementions 📏🐕
        🍦 y 🍺🔲🍺🐽 dementions 1 🚀
        🍦 vy 🍺🔲🍺🐽 dementions 7 🚀
        ✍ 🐕 ⚡ ➕ y vy ⚡ ⚡ ⚡ ⚡
    🍉
🍉
👴 There is no tennis ball emoji... so baseball it is
🐇 ⚾🔳 🍇
    🐖 📈 🍇
        🍦 dementions 📏🐕
        🍦 x 🍺🔲🍺🐽 dementions 0 🚀
        🍦 y 🍺🔲🍺🐽 dementions 1 🚀
        🍦 w 🍺🔲🍺🐽 dementions 2 🚂
        🍦 h 🍺🔲🍺🐽 dementions 3 🚂
        🍦 vx 🍺🔲🍺🐽 dementions 6 🚀
        🍮 vy 🍺🔲🍺🐽 dementions 7 🚀
        🍦 float_w 🚀 w
        🍦 float_h 🚀 h
        🍊 🎉 ▶️ ➕ y float_h 600.0 ◀️ y 0.0 🍇
            🍮 vy ➖ 0.0 vy
        🍉
        ✍ 🐕 ➕ x vx ➕ y vy ⚡ ⚡ vx vy
    🍉

🍉
🐋 🔳 🍇
    🐖 👁 b ⚾  🍇
        🍦 b_data 📏b
        🍦 b_x 🍺🔲🍺🐽 b_data 0 🚀
        🍦 b_y 🍺🔲🍺🐽 b_data 1 🚀
        🍦 b_hw 🍺🔲🍺🐽 b_data 4 🚂
        🍦 b_hh 🍺🔲🍺🐽 b_data 5 🚂
        🍊 ▶️ ➕ 🍩🚴🚄 y hh ➕ 🍩🚴🚄 b_y b_hh 🍇
            ✍ 🐕 ⚡ ⚡ ⚡ ⚡ ⚡ -0.15
        🍉 🍋 ◀️ ➕ 🍩🚴🚄 y hh ➕ 🍩🚴🚄 b_y b_hh 🍇
            ✍ 🐕 ⚡ ⚡ ⚡ ⚡ ⚡ 0.15
        🍉 🍓 🍇
            ✍ 🐕 ⚡ ⚡ ⚡ ⚡ ⚡ 0.0
        🍉
    🍉
🍉
