📦 SDL 🔴
📜 🔤game.emojic🔤
🐇🏃🍇
    🐇🐖 🏁 ➡️ 🚂 🍇
        🍩🎬 🕹
        🍦 winW 800
        🍦 winH 600
        🍦 playerA 🔷🎾🆕 50.0 350.0 26 100 🔤bat🔤 0.0 0.0
        🍦 playerB 🔷🎾🆕 725.0 350.0 26 100 🔤bat🔤 0.0 0.0
        🍦 ball 🔷⚾🆕 386.0 286.0 26 26 🔤ball🔤 0.1 0.1
        🍦 window 🔷🖼🆕 🔤Pong🔤 0 0 winW winH
        🍦 renderer 🔷🖌🆕 window
        🍮  quit 👎
        🔁 ❎quit 🍇
            👵👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴
            👴         INPUT  SECTION          👴
            👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👵
            🍦 event 🍩🎉 🕹
            🍊 ❎☁️event 🍇
                🍦 key_e 🔲 event ⌨
                🍊 ❎☁️key_e 🍇
                    🍊 🎉 😛 🔣🍺key_e 🔟q 😛 🔣🍺key_e 🔟q 🍇
                        🍮 quit 👍
                    🍉
                    ⌨ playerA 🍺key_e
                🍉
            🍉
            👵👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴
            👴         UPDATE SECTION          👴
            👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👵
            📈 playerA
            📈 ball
            🍦 ball_stuff 📏ball
            🍦 x 🍺🔲🍺🐽 ball_stuff 0 🚀
            🍦 w 🍺🔲🍺🐽 ball_stuff 2 🚂
            🍦 float_w 🚀 w
            🍊 ▶️ x 800.0 🍇
                👴 Point Player
                ✍ ball 386.0 286.0 ⚡ ⚡ 0.1 0.1
            🍉 🍋 ◀️ ➕ x float_w 0.0  🍇
                👴 Point Oppent
                ✍ ball 386.0 286.0 ⚡ ⚡ -0.1 -0.1
            🍉
            👁 playerB ball
            📈 playerB
            🛌 ball playerB
            🛌 ball playerA
            👵👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴
            👴         RENDER SECTION          👴
            👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👴👵
            🎨 renderer 0 0 0 255
            🚿 renderer
            🖌 playerA renderer
            🖌 playerB renderer
            🖌 ball renderer
            🍾 renderer
        🍉
        🍩⛳ 🕹
        🍎 0
    🍉
🍉
